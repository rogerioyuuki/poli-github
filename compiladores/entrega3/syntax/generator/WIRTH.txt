PROGRAMA = {DEFINICAO} MAIN {DEFINICAO} .

DEFINICAO = STRUCT | FUNCAO .

STRUCT = "BODYBUILDER" IDENTIFICADOR DECLARACAO_TIPO {DECLARACAO_TIPO} "BIRL" .

DECLARACAO_TIPO = TIPO IDENTIFICADOR ";" .

TIPO = "FRANGO" | "MONSTRINHO" | "MONSTRO" | "MONSTRAO" | "TRAPEZIO" | "TRAPEZIO" "DESCENDENTE" | "BICEPS" "FRANGO" | "BICEPS" "MONSTRINHO" | "BICEPS" "MONSTRO" | "BICEPS" "MONSTRAO" .

IDENTIFICADOR = LETRA {LETRA|DIGITO} .
LETRA =  "a" | "b" | "c" | "d" | "e" | "f"| "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A" | "B" | "C" | "D" | "E" | "F" | "G"  | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" .
DIGITO = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" .

FUNCAO = "OH" "O" "HOME" "AI" "PO" "(" TIPO IDENTIFICADOR "(" LISTA_PARAMETROS ")" ")" BLOCO_CODIGO "BIRL" .
LISTA_PARAMETROS = TIPO IDENTIFICADOR { "," TIPO IDENTIFICADOR } .

MAIN = "HORA" "DO" "SHOW" BLOCO_CODIGO "BIRL" .

BLOCO_CODIGO = COMANDO {COMANDO} .

COMANDO = COMANDO_ATRIBUICAO | CONDICIONAL | ITERATIVO | PULO .


COMANDO_ATRIBUICAO = TIPO ATRIBUICAO { "," ATRIBUICAO } ";" .
ATRIBUICAO = IDENTIFICADOR "=" EXPRESSAO | IDENTIFICADOR .

EXPRESSAO = EXPRESSAO_BOOL | EXPRESSAO_ARITM .

EXPRESSAO_BOOL = EXPRESSAO_BOOL OPERADOR_BOOL EXPRESSAO_BOOL | EXPRESSAO_ARITM OPERADOR_COMP EXPRESSAO_ARITM | IDENTIFICADOR | CHAMADA_FUNCAO | "FIBRA" | "AGUA" | "NEGATIVA" EXPRESSAO_BOOL | "(" EXPRESSAO_BOOL ")" .
EXPRESSAO_ARITM = EXPRESSAO_ARITM OPERADOR_ARITM EXPRESSAO_ARITM | IDENTIFICADOR | CHAMADA_FUNCAO | NUMERO | "(" EXPRESSAO_ARITM ")" .

OPERADOR_BOOL = "&&" | "||" .
OPERADOR_ARITM = "+" | "_" | "/" | "*" | "%" .
OPERADOR_COMP = ">" [ "=" ] | "<" [ "=" ] | "==" | "!=" .

NUMERO = DIGITO{DIGITO} | DIGITO{DIGITO} "." {DIGITO} | "." DIGITO {DIGITO} .

CHAMADA_FUNCAO = "AJUDA" "O" "MALUCO" "TA" "DOENTE" IDENTIFICADOR "(" [EXPRESSAO { "," EXPRESSAO } ] ")" . 

CONDICIONAL = IF | SWITCH .

IF = "ELE" "QUE" "A" "GENTE" "QUER" "?" "(" EXPRESSAO ")" BLOCO_CODIGO [ELSE_IF] BIRL .
ELSE_IF = "QUE" "NAO" "VAI" "DAR" "O" "QUE" "?" "(" EXPRESSAO ")" BLOCO_CODIGO [ELSE_IF] | "NAO" "VAI" "DAR" "NAO" BLOCO_CODIGO .

SWITCH = "DERRUBAR" "ARVORES" "(" EXPRESSAO ")" {CASE} BIRL .
CASE = "ARVORE" EXPRESSAO_CONSTANTE ":" BLOCO_CODIGO [CASE] | "ARVORE" "DO" "PARQUE" "DO" "IBIRAPUERA" ":" BLOCO_CODIGO .

EXPRESSAO_CONSTANTE = EXPRESSAO_BOOL_CONST | EXPRESSAO_ARITM_CONST .
EXPRESSAO_BOOL_CONST = EXPRESSAO_BOOL_CONST OPERADOR_BOOL EXPRESSAO_BOOL_CONST | EXPRESSAO_ARITM_CONST OPERADOR_COMP EXPRESSAO_ARITM_CONST | "FIBRA" | "AGUA" | "NEGATIVA" EXPRESSAO_BOOL_CONST | "(" EXPRESSAO_BOOL_CONST ")" .
EXPRESSAO_ARITM_CONST = EXPRESSAO_ARITM_CONST OPERADOR_ARITM EXPRESSAO_ARITM_CONST | NUMERO | "(" EXPRESSAO_ARITM_CONST ")" .

ITERATIVO = FOR | WHILE .
WHILE = "NEGATIVA" "BAMBAM (" EXPRESSAO ")" BLOCO_CODIGO "BIRL" .
FOR = "MAIS" "QUERO" "MAIS" "(" [EXPRESSAO] ";" [EXPRESSAO] ";" [EXPRESSAO] ")" BLOCO_CODIGO "BIRL" .

PULO = "BORA" "CUMPADE" EXPRESSAO ";" | "SAI" "FDP" ";" | "VAMO" "MONSTRO" ";" .
